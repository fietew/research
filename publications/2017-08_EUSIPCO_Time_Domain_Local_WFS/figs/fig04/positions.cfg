#!/usr/bin/gnuplot

#*****************************************************************************
# Copyright (c) 2013-2018 Fiete Winter                                       *
#                         Institut fuer Nachrichtentechnik                   *
#                         Universitaet Rostock                               *
#                         Richard-Wagner-Strasse 31, 18119 Rostock, Germany  *
#                                                                            *
# This file is part of the supplementary material for Fiete Winter's         *
# scientific work and publications                                           *
#                                                                            *
# You can redistribute the material and/or modify it  under the terms of the *
# GNU  General  Public  License as published by the Free Software Foundation *
# , either version 3 of the License,  or (at your option) any later version. *
#                                                                            *
# This Material is distributed in the hope that it will be useful, but       *
# WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY *
# or FITNESS FOR A PARTICULAR PURPOSE.                                       *
# See the GNU General Public License for more details.                       *
#                                                                            *
# You should  have received a copy of the GNU General Public License along   *
# with this program. If not, see <http://www.gnu.org/licenses/>.             *
#                                                                            *
# http://github.com/fietew/publications           fiete.winter@uni-rostock.de*
#*****************************************************************************

SPACING_HORIZONTAL = 0
SPACING_VERTICAL = 0

OUTER_RATIO_L = 0.5
OUTER_RATIO_R = 0.5
OUTER_RATIO_T = 0.5
OUTER_RATIO_B = 0.5

INNER_RATIO_H = 0.5
INNER_RATIO_V = 0.5

LABEL_X = 'x'
LABEL_Y = 'y'

# activate or deactivate axes labels
use_x = "set xtics format '\\footnotesize $%g$'; \
  set xlabel LABEL_X"
use_y = "set ytics format '\\footnotesize $%g$'; \
  set ylabel LABEL_Y"
no_x = "set xtics format ''; \
  set xlabel ''"
no_y = "set ytics format ''; \
  set ylabel ''"

# margins and spaces
MARGIN_L = "SPACING_HORIZONTAL*OUTER_RATIO_L";
MARGIN_R = "SPACING_HORIZONTAL*OUTER_RATIO_R";
MARGIN_T = "SPACING_VERTICAL*OUTER_RATIO_T";
MARGIN_B = "SPACING_VERTICAL*OUTER_RATIO_B";

OUTER_SPACE_L = "SPACING_HORIZONTAL*(1-OUTER_RATIO_L)"
OUTER_SPACE_R = "SPACING_HORIZONTAL*(1-OUTER_RATIO_R)"
OUTER_SPACE_T = "SPACING_VERTICAL*(1-OUTER_RATIO_T)"
OUTER_SPACE_B = "SPACING_VERTICAL*(1-OUTER_RATIO_B)"

INNER_SPACE_L = "SPACING_HORIZONTAL*INNER_RATIO_H";
INNER_SPACE_R = "SPACING_HORIZONTAL*(1-INNER_RATIO_H)";
INNER_SPACE_T = "SPACING_VERTICAL*INNER_RATIO_V";
INNER_SPACE_B = "SPACING_VERTICAL*(1-INNER_RATIO_V)";

margin_l = "set lmargin @MARGIN_L; set rmargin @OUTER_SPACE_L"
margin_r = "set lmargin @OUTER_SPACE_R; set rmargin @MARGIN_R"
margin_t = "set tmargin @MARGIN_T; set bmargin @OUTER_SPACE_T"
margin_b = "set tmargin @OUTER_SPACE_B; set bmargin @MARGIN_B"
margin_h = "set lmargin @INNER_SPACE_L; set rmargin @INNER_SPACE_R"
margin_v = "set tmargin @INNER_SPACE_T; set bmargin @INNER_SPACE_B"

# positioning
pos_top_left = "@margin_l; \
  @margin_t; \
  @no_x; \
  @use_y;"

pos_top = "@margin_h; \
  @margin_t; \
  @no_x; \
  @no_y; "

pos_top_right = "@margin_r; \
  @margin_t; \
  @no_x; \
  @no_y;"

pos_left = "@margin_l; \
  @margin_v; \
  @no_x; \
  @use_y;"

pos_mid = "@margin_h; \
  @margin_v; \
  @no_x; \
  @no_y;"

pos_right = "@margin_r; \
  @margin_v; \
  @no_x; \
  @no_y;"

pos_bottom_left = "@margin_l; \
  @margin_b; \
  @use_x; \
  @use_y;"

pos_bottom = "@margin_h; \
  @margin_b; \
  @use_x; \
  @no_y;"

pos_bottom_right = "@margin_r; \
  @margin_b; \
  @use_x; \
  @no_y;"
